apply plugin: 'java'
// enables 'install' task into repository
apply plugin: 'maven'
apply plugin: 'signing'
group ='com.researchspace'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = 1.8

ext.springVersion='4.3.0.RELEASE'
ext.spring_social_version ='1.1.4.RELEASE'
ext.jacksonVersion='2.6.7'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()      
}
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'org.springframework:spring-web:'+springVersion
    compile 'org.springframework:spring-core:'+springVersion
    compile 'com.fasterxml.jackson.core:jackson-annotations:'+jacksonVersion
    compile 'com.fasterxml.jackson.core:jackson-core:'+jacksonVersion
    compile 'com.fasterxml.jackson.core:jackson-databind:'+jacksonVersion
      
    compile 'org.projectlombok:lombok:1.16.10'
    compile 'commons-lang:commons-lang:2.6'
    compile 'commons-io:commons-io:2.5'
    compile 'org.springframework.social:spring-social-core:'+spring_social_version
    compile 'org.springframework.social:spring-social-config:'+spring_social_version
    
    compile 'org.slf4j:slf4j-api:1.7.21'
 
    testCompile   'junit:junit:4.12'
    testCompile   'org.springframework:spring-test:'+springVersion
    testCompile   'org.slf4j:slf4j-simple:1.7.21'
}

test{
 testLogging.showStandardStreams = true
 //delegate command line properties through to Test JVM
 systemProperties = System.getProperties()
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    systemProperties = System.getProperties()
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

//Lists dependencies
task listJars << {
    configurations.compile.each { File file -> println file.name }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
// build sources/javaoc only for release version
javadoc.onlyIf {isReleaseVersion}
sourcesJar.onlyIf {isReleaseVersion}
// Only sign if it's a release version
signing {
    required { isReleaseVersion  }
    sign configurations.archives
}


artifacts {
    archives javadocJar, sourcesJar
}


uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: hasProperty(ossrhUsername)?ossrhUsername:"", password: hasProperty(ossrhPassword)?ossrhPassword:"")
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: hasProperty(ossrhUsername)?ossrhUsername:"", password: hasProperty(ossrhPassword)?ossrhPassword:"")
      }

      pom.project {
        name 'figshare-client-java'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Java wrapper around the Figshare API'
        url 'https://github.com/rspace-os/figshare-client-java'

        scm {
          connection 'https://github.com/rspace-os/figshare-client-java.git'
          developerConnection 'https://github.com/rspace-os/figshare-client-java.git'
          url 'https://github.com/rspace-os/figshare-client-java'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'richard23'
            name 'Richard Adams'
            email 'richard@researchspace.com'
          }
        }
      }
    }
  }
}
